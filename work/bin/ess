#!/usr/bin/perl

use strict;
use warnings;

use XML::Simple;
use LWP::Simple;
use IO::All;
use Getopt::Long;

my $feed;
my %options;

Getopt::Long::Configure('bundling');
GetOptions(\%options,
	'pretend|p',
	'nocache|c',
	'help|h') or usage();

usage() if $options{help};

if (@ARGV == 1) {
    get_feed();
    my $show = shift;
    my @shows = select_show($show);
    if (@shows > 1) {
        print "Multiple shows found for criterion '$show'\n";
        exit 1;
    }
    my $link = $feed->{channel}{item}[$shows[0]]{link};
    $link =~ s/\/listen.pls$//;
	if ($options{pretend}) {
		print "mplayer -cache 512 '$link'\n";
	}
	else {
		exec "mplayer -cache 512 '$link'";
		die "couldn't fork mplayer: $!\n";
	}
}
elsif (@ARGV == 0) {
    get_feed();
    for (0..@{$feed->{channel}{item}}-1) {
        show_title($_);
    }
}
else {
    usage();
}

sub get_feed {
    return if $feed;
	my $xml;
	my $success = eval {
		print "Fetching RSS feed from ess.tv\n";
		$xml = get('http://ess.tv/ess.rss') or die "Fetch failed\n";
		$feed = XMLin($xml) or die "Parse failed\n";
		1;
	};
	if ($success) {
		$xml > io("$ENV{HOME}/.essrss.xml");
	}
	else {
		if ($options{nocache}) {
			print "Failed.  Caching disabled; retrying\n";
			goto &get_feed;
		}
		elsif (-e "$ENV{HOME}/.essrss.xml") {
			print "Failed.  Using cached copy of feed.\n";
			$xml = io("$ENV{HOME}/.essrss.xml")->slurp;
			$feed = XMLin($xml) or die "Parse failed (absurd!)\n";
		}
		else {
			print "Failed.  No cached copy found; retrying\n";
			goto &get_feed;
		}
	}
}

sub select_show {
    my ($crit) = @_;
    if ($crit =~ /^\d+$/) {
        return $crit-1;
    }
    else {
        my @matches;
        for my $i (0..@{$feed->{channel}{item}}-1) {
            if ($feed->{channel}{item}[$i]{title} =~ /$crit/i) {
                push @matches, $i;
            }
        }
        if (@matches == 0) {
            print "No match found for '$crit'\n";
            exit 1;
        }
        else {
            return @matches;
        }
    }
}

sub show_title {
    my ($num) = @_;
    printf "ESS %02d: %s\n", $num+1, $feed->{channel}{item}[$num]{title}, "\n";
}

sub usage {
    print <<EOU;
Usage: ess                   Print a schedule
       ess <number>          Watch that stream number
       ess <string>          Watch the show whose title contains that string
  Options:
       -p --pretend          Print the command that would be executed 
                                 instead of executing it
       -c --nocache          Turn off caching when the feed is broken
       -h --help             This help message
EOU
    exit;
}

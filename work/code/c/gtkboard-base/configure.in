
AC_INIT(src/)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gtkboard,0.11pre0)
AC_PROG_CC

AC_ARG_ENABLE(gtk2,
		[  --disable-gtk2      Don't look for GTK2 libraries],
		enable_gtk2=$enableval,
		enable_gtk2="yes")

found_gtk2=0;

if test "$enable_gtk2" = "yes" ; then
	dnl check if user has gtk2
	PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.0.0, found_gtk2=1, found_gtk2=0)
fi

if test $found_gtk2 = 0; then
	AM_PATH_GTK(1.2.3, , AC_MSG_ERROR(gtkboard needs GTK 1.2.3 or higher))
fi


AC_ARG_ENABLE(sdl,
        [  --disable-sdl      Don't look for the SDL library],
        enable_sdl=$enableval,
        enable_sdl="yes")

if test "$enable_sdl" = "yes" ; then
	dnl Check for SDL
	SDL_VERSION=1.2.0
	AM_PATH_SDL($SDL_VERSION,
			AC_DEFINE([HAVE_SDL], [], [Do we have the SDL library]), 
			AC_MSG_WARN([*** SDL version $SDL_VERSION not found. Sound will be disabled])
	)
	CFLAGS="$CFLAGS $SDL_CFLAGS"
	LIBS="$LIBS $SDL_LIBS -lSDL_mixer"
fi

gnome=false

GNOME_CFLAGS=""
GNOME_LIBS=""

AC_ARG_ENABLE(gnome,[  --enable-gnome    Turn on gnome ],[
	if test "x$enableval" = "xyes"; then
		gnome=true
	fi
])

if test "$gnome" = true ; then
	PKG_CHECK_MODULES(GNOME, [
			libgnome-2.0 >= 2.0.0
			libgnomeui-2.0 >= 2.0.0
	])
	GNOME_CFLAGS="$GNOME_CFLAGS -DHAVE_GNOME=1"
fi

AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)
	
AM_CONDITIONAL(WITH_GNOME, test "x$gnome" = "xtrue")

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(mkdir strtol)

AC_OUTPUT([
		Makefile
		doc/Makefile
		src/Makefile
		sounds/Makefile
		pixmaps/Makefile
		])

echo " "

if test "$enable_gtk2" = "yes" ; then
	AC_MSG_RESULT([    GTK2 enabled.])
else
	AC_MSG_RESULT([    GTK2 disabled.])
fi

if test "$enable_sdl" = "yes" ; then
	AC_MSG_RESULT([     SDL enabled.])
else
	AC_MSG_RESULT([     SDL disabled.])
fi
if test "$enable_gnome" = "yes" ; then
	AC_MSG_RESULT([   GNOME enabled.])
else
	AC_MSG_RESULT([   GNOME disabled.])
fi

